tasks.named("compileKotlin") {
    dependsOn(generateAllTask)
}

val generateAllTask = tasks.register("generate-all") {
    group = TASK_GROUP
    dependsOn(
        syncUnmodifiableTask,
        pairComponentNTask,
        pairFactoryTask,
        immutableListSetFactoryTask,
        emptyAndSingletonMapFactoryTask,
        mutableListFactoryTask,
        mutableSetFactoryTask,
        mutableMapFactoryTask,
        mapFastIterableIteratorTask,
        arrayMapToTypedArrayTask,
        collectionMapToTypedArrayTask,
        typedIterableForEachTask,
        arrayAndListSwapTask,
        enumSetTask,
        enumMapTask,
        forEachIsInstanceTask,
        filterIsInstanceToTask,
        functionInvokeTask,
        predicateInvokeTask,
        consumerInvokeTask,
        unaryOperatorInvokeTask,
        binaryOperatorInvokeTask,
    )
}

/**
 * Example:
 * - `IntList.synchronized()`
 * - `IntList.synchronized(lock)`
 * - `IntList.unmodifiable()`
 */
val syncUnmodifiableTask = tasks.register<GenerateSrcTask>("sync-unmodifiable") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.add("java.util.*")
    imports.add("it.unimi.dsi.fastutil.PriorityQueue")
    imports.add("it.unimi.dsi.fastutil.PriorityQueues")
    imports.addAll(IMPORT_ALL)

    content {
        // For java.util.Collections
        // Java 21+ -> "SequencedCollection", "SequencedMap", "SequencedSet"
        for (rawType in arrayOf("Collection", "List", "Map", "Set", "NavigableMap", "NavigableSet", "SortedMap", "SortedSet")) {
            val ktType = if (rawType in arrayOf("Collection", "List", "Map", "Set")) "Mutable$rawType" else rawType
            if (rawType.endsWith("Map")) {
                appendLine("inline fun <K, V> ${ktType}<K, V>.unmodifiable(): ${rawType}<K, V> = Collections.unmodifiable${rawType}(this)")
                appendLine("inline fun <K, V> ${ktType}<K, V>.synchronized(): ${ktType}<K, V> = Collections.synchronized${rawType}(this)")
            } else {
                appendLine("inline fun <T> ${ktType}<T>.unmodifiable(): ${rawType}<T> = Collections.unmodifiable${rawType}(this)")
                appendLine("inline fun <T> ${ktType}<T>.synchronized(): ${ktType}<T> = Collections.synchronized${rawType}(this)")
            }
        }

        forEachTypes { type ->
            for (suffix in arrayOf("BigList", "List", "Set", "Collection")) {
                val rawType = type.typeName + suffix
                if (type.isGeneric) {
                    appendLine("inline fun <T> ${rawType}<T>.synchronized(): ${rawType}<T> = ${rawType}s.synchronize(this)")
                    appendLine("inline fun <T> ${rawType}<T>.synchronized(lock: Any): ${rawType}<T> = ${rawType}s.synchronize(this, lock)")
                    appendLine("inline fun <T> ${rawType}<T>.unmodifiable(): ${rawType}<T> = ${rawType}s.unmodifiable(this)")
                } else {
                    appendLine("inline fun ${rawType}.synchronized(): $rawType = ${rawType}s.synchronize(this)")
                    appendLine("inline fun ${rawType}.synchronized(lock: Any): $rawType = ${rawType}s.synchronize(this, lock)")
                    appendLine("inline fun ${rawType}.unmodifiable(): $rawType = ${rawType}s.unmodifiable(this)")
                }
            }
        }

        appendLine("inline fun <T> PriorityQueue<T>.synchronized(): PriorityQueue<T> = PriorityQueues.synchronize(this)")
        appendLine("inline fun <T> PriorityQueue<T>.synchronized(lock: Any): PriorityQueue<T> = PriorityQueues.synchronize(this, lock)")
        forEachTypes { type ->
            if (type.isGeneric || type == FastutilType.BOOLEAN) {
                return@forEachTypes
            } else {
                val rawType = type.typeName + "PriorityQueue"
                appendLine("inline fun ${rawType}.synchronized(): $rawType = ${rawType}s.synchronize(this)")
                appendLine("inline fun ${rawType}.synchronized(lock: Any): $rawType = ${rawType}s.synchronize(this, lock)")
            }
        }

        forEachMapTypes { left, right ->
            val rawType = "${left}2${right}Map"
            when {
                left.isGeneric && right.isGeneric -> {
                    appendLine("inline fun <K, V> ${rawType}<K, V>.synchronized(): ${rawType}<K, V> = ${rawType}s.synchronize(this)")
                    appendLine("inline fun <K, V> ${rawType}<K, V>.synchronized(lock: Any): ${rawType}<K, V> = ${rawType}s.synchronize(this, lock)")
                    appendLine("inline fun <K, V> ${rawType}<K, V>.unmodifiable(): ${rawType}<K, V> = ${rawType}s.unmodifiable(this)")
                }

                left.isGeneric -> {
                    appendLine("inline fun <K> ${rawType}<K>.synchronized(): ${rawType}<K> = ${rawType}s.synchronize(this)")
                    appendLine("inline fun <K> ${rawType}<K>.synchronized(lock: Any): ${rawType}<K> = ${rawType}s.synchronize(this, lock)")
                    appendLine("inline fun <K> ${rawType}<K>.unmodifiable(): ${rawType}<K> = ${rawType}s.unmodifiable(this)")
                }

                right.isGeneric -> {
                    appendLine("inline fun <V> ${rawType}<V>.synchronized(): ${rawType}<V> = ${rawType}s.synchronize(this)")
                    appendLine("inline fun <V> ${rawType}<V>.synchronized(lock: Any): ${rawType}<V> = ${rawType}s.synchronize(this, lock)")
                    appendLine("inline fun <V> ${rawType}<V>.unmodifiable(): ${rawType}<V> = ${rawType}s.unmodifiable(this)")
                }

                else -> {
                    appendLine("inline fun ${rawType}.synchronized(): $rawType = ${rawType}s.synchronize(this)")
                    appendLine("inline fun ${rawType}.synchronized(lock: Any): $rawType = ${rawType}s.synchronize(this, lock)")
                    appendLine("inline fun ${rawType}.unmodifiable(): $rawType = ${rawType}s.unmodifiable(this)")
                }
            }
        }
    }
}

/**
 * Example:
 * - `val (left, right) = IntCharPair.of(1, 'c')`
 */
val pairComponentNTask = tasks.register<GenerateSrcTask>("pair-componentN") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.add("it.unimi.dsi.fastutil.Pair")
    imports.addAll(IMPORT_ALL)

    content {
        appendLine("inline operator fun <K, V> Pair<K, V>.component1() = left()")
        appendLine("inline operator fun <K, V> Pair<K, V>.component2() = right()")

        forEachTypes { left ->
            forEachTypes { right ->
                when {
                    left.isGeneric && right.isGeneric -> if (left != FastutilType.OBJECT && right != FastutilType.OBJECT) { // ObjectObjectPair does not exist
                        appendLine("inline operator fun <K, V> ${left}${right}Pair<K, V>.component1() = left()")
                        appendLine("inline operator fun <K, V> ${left}${right}Pair<K, V>.component2() = right()")
                    }

                    left.isGeneric -> {
                        appendLine("inline operator fun <K> ${left}${right}Pair<K>.component1() = left()")
                        appendLine("inline operator fun ${left}${right}Pair<*>.component2() = right${right}()")
                    }

                    right.isGeneric -> {
                        appendLine("inline operator fun ${left}${right}Pair<*>.component1() = left${left}()")
                        appendLine("inline operator fun <V> ${left}${right}Pair<V>.component2() = right()")
                    }

                    else -> {
                        appendLine("inline operator fun ${left}${right}Pair.component1() = left${left}()")
                        appendLine("inline operator fun ${left}${right}Pair.component2() = right${right}()")
                    }
                }
            }
        }
    }
}

val pairFactoryTask = tasks.register<GenerateSrcTask>("pair-factory") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
//    imports.add("it.unimi.dsi.fastutil.Pair")
    imports.addAll(IMPORT_ALL)

    content {
        // Left has no OBJECT/REFERENCE to prevent autocompletion polluted
        forEachPrimitiveTypes { typeLeft ->
            forEachTypes { typeRight ->
                if (typeRight.isGeneric) {
                    if (typeRight == FastutilType.OBJECT) {
                        appendLine("inline infix fun <V> ${typeLeft}.pair(right: V) = ${typeLeft}${typeRight}ImmutablePair(this, right)")
                        appendLine("inline infix fun <V> ${typeLeft}.mutPair(right: V) = ${typeLeft}${typeRight}MutablePair(this, right)")
                    } else {
                        appendLine("inline infix fun <V> ${typeLeft}.refPair(right: V) = ${typeLeft}${typeRight}ImmutablePair(this, right)")
                        appendLine("inline infix fun <V> ${typeLeft}.mutRefPair(right: V) = ${typeLeft}${typeRight}MutablePair(this, right)")
                    }
                } else {
                    appendLine("inline infix fun ${typeLeft}.pair(right: ${typeRight}) = ${typeLeft}${typeRight}ImmutablePair(this, right)")
                    appendLine("inline infix fun ${typeLeft}.mutPair(right: ${typeRight}) = ${typeLeft}${typeRight}MutablePair(this, right)")
                }
            }
        }
    }
}

/**
 * Example:
 * - `intListOf(...)
 * - `intArrayOf(...).asIntList()`
 */
val immutableListSetFactoryTask = tasks.register<GenerateSrcTask>("immutable-list-set-factory") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)

    content {
        forEachTypes { type ->
            val emptyList = "${type}Lists.emptyList()"
            val emptySet = "${type}Sets.emptySet()"
            fun singletonList(placeholder: String = "element") = "${type}Lists.singleton($placeholder)"
            fun singletonSet(placeholder: String = "element") = "${type}Sets.singleton($placeholder)"
            if (type.isGeneric) {
                appendLine("inline fun <T> ${type}List<T>?.orEmpty(): ${type}List<T> = this ?: $emptyList")
                appendLine("inline fun <T> ${type}Set<T>?.orEmpty(): ${type}Set<T> = this ?: $emptySet")

                appendLine("inline fun <T> ${type.lowercaseName}ListOf(): ${type}List<T> = $emptyList")
                appendLine("inline fun <T> ${type.lowercaseName}SetOf(): ${type}Set<T> = $emptySet")

                appendLine("inline fun <T> ${type.lowercaseName}ListOf(element: T): ${type}List<T> = ${singletonList()}")
                appendLine("inline fun <T> ${type.lowercaseName}SetOf(element: T): ${type}Set<T> = ${singletonSet()}")

                appendLine("inline fun <T> ${type.lowercaseName}ListOf(vararg elements: T): ${type}List<T> =")
                appendLine("when(elements.size) { 0 -> $emptyList 1 -> ${singletonList("elements[0]")} else -> ${type}ImmutableList(elements) }")

                appendLine("inline fun <T> Array<out T>.as${type}List(): ${type}List<T> = ${type}ImmutableList(this)")
                appendLine("inline fun <T> Array<out T>.as${type}List(offset: Int = 0, length: Int = this.size): ${type}List<T> = ${type}ImmutableList(this, offset, length)")
            } else {
                appendLine("inline fun ${type}List?.orEmpty(): ${type}List = this ?: $emptyList")
                appendLine("inline fun ${type}Set?.orEmpty(): ${type}Set = this ?: $emptySet")

                appendLine("inline fun ${type.lowercaseName}ListOf(): ${type}List = $emptyList")
                appendLine("inline fun ${type.lowercaseName}SetOf(): ${type}Set = $emptySet")

                appendLine("inline fun ${type.lowercaseName}ListOf(element: ${type}): ${type}List = ${singletonList()}")
                appendLine("inline fun ${type.lowercaseName}SetOf(element: ${type}): ${type}Set = ${singletonSet()}")

                appendLine("inline fun ${type.lowercaseName}ListOf(vararg elements: ${type}): ${type}List =")
                appendLine("when(elements.size) { 0 -> $emptyList 1 -> ${singletonList("elements[0]")} else -> ${type}ImmutableList(elements) }")

                appendLine("inline fun ${type}Array.as${type}List(): ${type}List = ${type}ImmutableList(this)")
                appendLine("inline fun ${type}Array.as${type}List(offset: Int = 0, length: Int = this.size): ${type}List = ${type}ImmutableList(this, offset, length)")
            }
            appendLine()
        }
    }
}

/**
 * Example:
 * - `intMutableListOf(...)`
 * - `intArrayOf(...).toIntMutableList()`
 */
val mutableListFactoryTask = tasks.register<GenerateSrcTask>("mutable-list-factory") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)

    content {
        forEachTypes { type ->
            if (type.isGeneric) {
                appendLine("inline fun <T> ${type.lowercaseName}MutableListOf(): ${type}List<T> = ${type}ArrayList()")
                appendLine("inline fun <T> ${type.lowercaseName}MutableListOf(vararg elements: T): ${type}List<T> = ${type}ArrayList(elements)")

                appendLine("inline fun <T> Array<out T>.to${type}MutableList(offset: Int = 0, length: Int = this.size): ${type}List<T> = ${type}ArrayList(this, offset, length)")

                appendLine("inline fun <T> ${type.lowercaseName}ArrayListOf(): ${type}ArrayList<T> = ${type}ArrayList()")
                appendLine("inline fun <T> ${type.lowercaseName}ArrayListOf(vararg elements: T): ${type}ArrayList<T> = ${type}ArrayList(elements)")

                appendLine("inline fun <T> Array<out T>.to${type}ArrayList(offset: Int = 0, length: Int = this.size): ${type}ArrayList<T> = ${type}ArrayList(this, offset, length)")
            } else {
                appendLine("inline fun ${type.lowercaseName}MutableListOf(): ${type}List = ${type}ArrayList()")
                appendLine("inline fun ${type.lowercaseName}MutableListOf(element: ${type}): ${type}List = ${type}ArrayList.wrap(${type.lowercaseName}ArrayOf(element))")
                appendLine("inline fun ${type.lowercaseName}MutableListOf(vararg elements: ${type}): ${type}List = ${type}ArrayList(elements)")

                appendLine("inline fun ${type}Array.to${type}MutableList(offset: Int = 0, length: Int = this.size): ${type}List = ${type}ArrayList(this, offset, length)")

                appendLine("inline fun ${type.lowercaseName}ArrayListOf(): ${type}ArrayList = ${type}ArrayList()")
                appendLine("inline fun ${type.lowercaseName}ArrayListOf(element: ${type}): ${type}ArrayList = ${type}ArrayList.wrap(${type.lowercaseName}ArrayOf(element))")
                appendLine("inline fun ${type.lowercaseName}ArrayListOf(vararg elements: ${type}): ${type}ArrayList = ${type}ArrayList(elements)")

                appendLine("inline fun ${type}Array.to${type}ArrayList(offset: Int = 0, length: Int = this.size): ${type}ArrayList = ${type}ArrayList(this, offset, length)")
            }
        }
    }
}

/**
 * Example:
 * - `intArraySetOf(...)`
 * - `intHashSetOf(...)`
 * - `intHashSetOf(strategy, ...)`
 * - `intLinkedSetOf(...)`
 * - `intRBTreeSetOf(...)`
 * - `intAVLTreeSetOf(...)`
 */
val mutableSetFactoryTask = tasks.register<GenerateSrcTask>("mutable-set-factory") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)
    imports.add("it.unimi.dsi.fastutil.Hash")

    content {
        val prefixToFullType = mapOf(
            "Array" to "ArraySet",
            "Hash" to "OpenHashSet",
            "Linked" to "LinkedOpenHashSet",
            "RBTree" to "RBTreeSet",
            "AVLTree" to "AVLTreeSet",
        )
        forEachPrimitiveTypes { type ->
            for ((prefix, fullType) in prefixToFullType) {
                val setType = "${prefix}Set"
                if (type != FastutilType.BOOLEAN && (prefix == "RBTree" || prefix == "AVLTree") || (prefix == "Array" || prefix == "Hash")) {
                    appendLine("inline fun ${type.lowercaseName}${setType}Of(): ${type}${fullType} = ${type}${fullType}()")
                    when (prefix) {
                        "Array", "RBTree", "AVLTree" -> appendLine("inline fun ${type.lowercaseName}${setType}Of(vararg elements: ${type}): ${type}${fullType} = ${type}${fullType}(elements)")
                        "Hash" -> {
                            appendLine("inline fun ${type.lowercaseName}${setType}Of(element: ${type}): ${type}${fullType} = ${type}${fullType}.of(element)")
                            appendLine("inline fun ${type.lowercaseName}${setType}Of(vararg elements: ${type}): ${type}${fullType} = ${type}${fullType}(elements)")

                            if (type != FastutilType.BOOLEAN)
                                appendLine("inline fun ${type.lowercaseName}${setType}Of(strategy: ${type}Hash.Strategy, vararg elements: ${type}): ${type}OpenCustomHashSet = ${type}OpenCustomHashSet(elements, strategy)")
                        }

                        else -> {
                            appendLine("inline fun ${type.lowercaseName}${setType}Of(element: ${type}): ${type}${fullType} = ${type}${fullType}.of(element)")
                            appendLine("inline fun ${type.lowercaseName}${setType}Of(vararg elements: ${type}): ${type}${fullType} = ${type}${fullType}(elements)")
                        }
                    }
                }
            }
        }

        var type = FastutilType.OBJECT
        for ((prefix, fullType) in prefixToFullType) {
            val setType = "${prefix}Set"
            when (prefix) {
                "Array" -> {
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(): ${type}${fullType}<T> = ${type}${fullType}()")
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(vararg elements: T): ${type}${fullType}<T> = ${type}${fullType}(elements)")
                }

                "RBTree", "AVLTree" -> {
                    appendLine("inline fun <T : Comparable<T>> ${type.lowercaseName}${setType}Of(): ${type}${fullType}<T> = ${type}${fullType}()")
                    appendLine("inline fun <T : Comparable<T>> ${type.lowercaseName}${setType}Of(vararg elements: T): ${type}${fullType}<T> = ${type}${fullType}(elements)")

                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(comparator: Comparator<in T>): ${type}${fullType}<T> = ${type}${fullType}(comparator)")
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(comparator: Comparator<in T>, vararg elements: T): ${type}${fullType}<T> = ${type}${fullType}(elements, comparator)")
                }

                "Hash" -> {
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(): ${type}${fullType}<T> = ${type}${fullType}()")
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(element: T): ${type}${fullType}<T> = ${type}${fullType}.of(element)")
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(vararg elements: T): ${type}${fullType}<T> = ${type}${fullType}(elements)")

                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(strategy: Hash.Strategy<T>, vararg elements: T): ${type}OpenCustomHashSet<T> = ${type}OpenCustomHashSet(elements, strategy)")
                }

                else -> {
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(element: T): ${type}${fullType}<T> = ${type}${fullType}.of(element)")
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(vararg elements: T): ${type}${fullType}<T> = ${type}${fullType}(elements)")
                }
            }
        }

        type = FastutilType.REFERENCE
        for ((prefix, fullType) in prefixToFullType) {
            val setType = "${prefix}Set"
            when (prefix) {
                "Array" -> appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(vararg elements: T): ${type}${fullType}<T> = ${type}${fullType}(elements)")
                "RBTree", "AVLTree" -> {}
                "Hash" -> {
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(element: T): ${type}${fullType}<T> = ${type}${fullType}.of(element)")
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(vararg elements: T): ${type}${fullType}<T> = ${type}${fullType}(elements)")
                }

                else -> {
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(element: T): ${type}${fullType}<T> = ${type}${fullType}.of(element)")
                    appendLine("inline fun <T> ${type.lowercaseName}${setType}Of(vararg elements: T): ${type}${fullType}<T> = ${type}${fullType}(elements)")
                }
            }
        }
    }
}

/**
 * intBooleanMapOf()
 * intBooleanMapOf(1, true)
 */
val emptyAndSingletonMapFactoryTask = tasks.register<GenerateSrcTask>("empty-and-singleton-map-factory") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)

    content {
        forEachMapTypes { left, right ->
            val genericType = when {
                left.isGeneric && right.isGeneric -> "<K, V>"
                left.isGeneric -> "<K>"
                right.isGeneric -> "<V>"
                else -> null
            }

            val keyType = if (left.isGeneric) "K" else left.typeName
            val valueType = if (right.isGeneric) "V" else right.typeName

            val functionName = left.lowercaseName + right.typeName + "MapOf"
            val mapTypeName = left.typeName + "2" + right.typeName + "Map"
            val mapTypeNameWithGeneric = if (genericType != null) mapTypeName + genericType else mapTypeName

            append("inline fun ")
            if (genericType != null) {
                append(genericType)
                space()
            }
            append("$functionName(): $mapTypeNameWithGeneric = ${mapTypeName}s.")
            if (genericType != null) {
                appendLine("emptyMap()")
            } else {
                appendLine("EMPTY_MAP")
            }

            append("inline fun ")
            if (genericType != null) {
                append(genericType)
                space()
            }
            appendLine("$functionName(k: $keyType, v: $valueType): $mapTypeNameWithGeneric = ${mapTypeName}s.singleton(k, v)")
        }
    }
}

/**
 * intLongArrayMapOf
 * intObjectHashMapOf
 * longBooleanLinkedMapOf
 */
val mutableMapFactoryTask = tasks.register<GenerateSrcTask>("mutable-map-factory") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)
//    imports.add("it.unimi.dsi.fastutil.Hash")

    content {
        val prefixToFullType = mapOf(
            "Array" to "ArrayMap",
            "Hash" to "OpenHashMap",
            "Linked" to "LinkedOpenHashMap",
//            "RBTree" to "RBTreeMap", TODO: sorted map, custom map...
//            "AVLTree" to "AVLTreeMap",
        )

        forEachMapTypes { left, right ->
            val genericType = when {
                left.isGeneric && right.isGeneric -> "<K, V>"
                left.isGeneric -> "<K>"
                right.isGeneric -> "<V>"
                else -> null
            }

            val keyType = if (left.isGeneric) "K" else left.typeName
            val valueType = if (right.isGeneric) "V" else right.typeName

            for ((prefix, mapType) in prefixToFullType) {
                for (i in 0..PARAM_ENUMERATION_COUNT) {
                    append("inline fun ")
                    if (genericType != null) {
                        append(genericType)
                        space()
                    }
                    // function name
                    val functionName = left.lowercaseName + right.typeName + prefix + "MapOf"
                    val mapTypeName = left.typeName + "2" + right.typeName + mapType
                    val mapTypeNameWithGeneric = if (genericType != null) mapTypeName + genericType else mapTypeName
                    append(functionName)
                    appendLine("(")
                    withIndent {
                        repeat(i) {
                            appendLine("k$it: $keyType, v$it: $valueType,")
                        }
                    }
                    appendLine("): $mapTypeNameWithGeneric {")

                    withIndent {
                        if (i == 0) {
                            appendLine("val map = $mapTypeNameWithGeneric()")
                        } else {
                            appendLine("val map = $mapTypeNameWithGeneric($i)")
                        }

                        repeat(i) {
                            appendLine("map.put(k$it, v$it)")
                        }

                        appendLine("return map")
                    }

                    appendLine("}")
                    appendLine()
                }
            }
        }
    }
}

/**
 * Example:
 * - `Int2ObjectMap.fastIterable()`
 * - `Int2ObjectMap.fastIterator()`
 * - `Int2ObjectMap.fastForEach { entry -> ... }`
 * - `Int2ObjectMap.fastForEach { k, v -> ... }`
 */
val mapFastIterableIteratorTask = tasks.register<GenerateSrcTask>("map-fast") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.add("java.util.function.Consumer")
    imports.addAll(IMPORT_ALL)

    content {
        forEachMapTypes { left, right ->
            val rawType = "${left}2${right}Map"
            val entryRawType = "${rawType}.Entry"
            when {
                left.isGeneric && right.isGeneric -> {
                    appendLine("inline fun <K, V> ${rawType}<K, V>.fastIterable(): ObjectIterable<${entryRawType}<K, V>> = ${rawType}s.fastIterable(this)")
                    appendLine("inline fun <K, V> ${rawType}<K, V>.fastIterator(): ObjectIterator<${entryRawType}<K, V>> = ${rawType}s.fastIterator(this)")
                    appendLine("inline fun <K, V> ${rawType}<K, V>.fastForEach(consumer: Consumer<${entryRawType}<K, V>>) = ${rawType}s.fastForEach(this, consumer)")
                    appendLine("inline fun <K, V> ${rawType}<K, V>.fastForEach(crossinline action: (key: K, value: V) -> Unit) = fastForEach { action(it.key, it.value) }")
                }

                left.isGeneric -> {
                    appendLine("inline fun <K> ${rawType}<K>.fastIterable(): ObjectIterable<${entryRawType}<K>> = ${rawType}s.fastIterable(this)")
                    appendLine("inline fun <K> ${rawType}<K>.fastIterator(): ObjectIterator<${entryRawType}<K>> = ${rawType}s.fastIterator(this)")
                    appendLine("inline fun <K> ${rawType}<K>.fastForEach(consumer: Consumer<${entryRawType}<K>>) = ${rawType}s.fastForEach(this, consumer)")
                    appendLine("inline fun <K> ${rawType}<K>.fastForEach(crossinline action: (key: K, value: $right) -> Unit) = fastForEach { action(it.key, it.${right.lowercaseName}Value) }")
                }

                right.isGeneric -> {
                    appendLine("inline fun <V> ${rawType}<V>.fastIterable(): ObjectIterable<${entryRawType}<V>> = ${rawType}s.fastIterable(this)")
                    appendLine("inline fun <V> ${rawType}<V>.fastIterator(): ObjectIterator<${entryRawType}<V>> = ${rawType}s.fastIterator(this)")
                    appendLine("inline fun <V> ${rawType}<V>.fastForEach(consumer: Consumer<${entryRawType}<V>>) = ${rawType}s.fastForEach(this, consumer)")
                    appendLine("inline fun <V> ${rawType}<V>.fastForEach(crossinline action: (key: $left, value: V) -> Unit) = fastForEach { action(it.${left.lowercaseName}Key, it.value) }")
                }

                else -> {
                    appendLine("inline fun ${rawType}.fastIterable(): ObjectIterable<${entryRawType}> = ${rawType}s.fastIterable(this)")
                    appendLine("inline fun ${rawType}.fastIterator(): ObjectIterator<${entryRawType}> = ${rawType}s.fastIterator(this)")
                    appendLine("inline fun ${rawType}.fastForEach(consumer: Consumer<${entryRawType}>) = ${rawType}s.fastForEach(this, consumer)")
                    appendLine("inline fun ${rawType}.fastForEach(crossinline action: (key: $left, value: $right) -> Unit) = fastForEach { action(it.${left.lowercaseName}Key, it.${right.lowercaseName}Value) }")
                }
            }
        }
    }
}

/**
 * Example:
 * - `arrayOf("xxx").mapToIntArray { it.hashCode() }`
 * - `intArrayOf(...).mapToFloatArray { it * 0.5f }.asFloatList()`
 */
val arrayMapToTypedArrayTask = tasks.register<GenerateSrcTask>("array-map-to-typed-array") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)

    content {
        // Array map to object array
        appendLine("inline fun <T, reified R> Array<out T>.mapToArray(transform: (T) -> R): Array<R> = Array(this.size) { i -> transform(this[i]) }")
        forEachPrimitiveTypes { type ->
            appendLine("inline fun <reified R> ${type}Array.mapToArray(transform: (${type}) -> R): Array<R> = Array(this.size) { i -> transform(this[i]) }")
        }
        // Array map to primitive array
        forEachPrimitiveTypes { result ->
            appendLine("inline fun <T> Array<out T>.mapTo${result}Array(transform: (T) -> ${result}): ${result}Array = ${result}Array(this.size) { i -> transform(this[i]) }")
            forEachPrimitiveTypes { receiver ->
                appendLine("inline fun ${receiver}Array.mapTo${result}Array(transform: (${receiver}) -> ${result}): ${result}Array = ${result}Array(this.size) { i -> transform(this[i]) }")
            }
        }
    }
}

val collectionMapToTypedArrayTask = tasks.register<GenerateSrcTask>("collection-map-to-typed-array") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)

    content {
        val iteratorName = "i"

        // Collection map to object array
        appendLine("inline fun <T, reified R> Collection<T>.mapToArray(transform: (T) -> R): Array<R> {")
        appendLine("    val $iteratorName = iterator()")
        appendLine("    return Array(size) { transform($iteratorName.next()) }")
        appendLine("}")
        forEachPrimitiveTypes { type ->
            appendLine("inline fun <reified R> ${type}Collection.mapToArray(transform: (${type}) -> R): Array<R> {")
            appendLine("    val $iteratorName = iterator()")
            appendLine("    return Array(size) { transform($iteratorName.next${type}()) }")
            appendLine("}")
        }
        // Collection map to primitive array
        forEachPrimitiveTypes { result ->
            appendLine("inline fun <T> Collection<T>.mapTo${result}Array(transform: (T) -> ${result}): ${result}Array {")
            appendLine("    val $iteratorName = iterator()")
            appendLine("    return ${result}Array(size) { transform($iteratorName.next()) }")
            appendLine("}")
            forEachPrimitiveTypes { receiver ->
                appendLine("inline fun ${receiver}Collection.mapTo${result}Array(transform: (${receiver}) -> ${result}): ${result}Array {")
                appendLine("    val $iteratorName = iterator()")
                appendLine("    return ${result}Array(size) { transform($iteratorName.next${receiver}()) }")
                appendLine("}")
            }
        }
    }
}

/**
 * Example:
 * - `intListOf().forEachInt { println(it) }`
 */
val typedIterableForEachTask = tasks.register<GenerateSrcTask>("typed-iterable-forEach") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)

    content {
        forEachPrimitiveTypes { type ->
            // Iterator forEach (full name)
            appendLine("inline fun ${type.packageName}.${type}Iterator.forEach${type}(action: (${type}) -> Unit) {")
            appendLine("    while (hasNext()) action(next$type())")
            appendLine("}")

            // Iterable forEach
            appendLine("inline fun ${type}Iterable.forEach${type}(action: (${type}) -> Unit) = iterator().forEach$type(action)")

            // Iterable onEach
            appendLine("inline fun <T : ${type}Iterable> T.onEach${type}(action: (${type}) -> Unit): T {")
            appendLine("    forEach$type(action)")
            appendLine("    return this")
            appendLine("}")

            // Iterable forEachIndexed
            appendLine("inline fun ${type}Iterable.forEach${type}Indexed(action: (index: Int, ${type}) -> Unit) {")
            appendLine("    var i = 0")
            appendLine("    forEach$type { action(i++, it) }")
            appendLine("}")
        }
    }
}

/**
 * Example:
 * - `intListOf().swap(1, 2)`
 */
val arrayAndListSwapTask = tasks.register<GenerateSrcTask>("array-and-list-swap") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)

    content {
        // Array
        appendLine("inline fun <T> Array<T>.swap(i: Int, j: Int) { val t = this[j]; this[j] = this[i]; this[i] = t }")
        forEachPrimitiveTypes { type ->
            appendLine("inline fun ${type}Array.swap(i: Int, j: Int) { val t = this[j]; this[j] = this[i]; this[i] = t }")
        }

        // List
        appendLine("inline fun <T> MutableList<T>.swap(i: Int, j: Int) { val t = this[j]; this[j] = this[i]; this[i] = t }")
        forEachPrimitiveTypes { type ->
            appendLine("inline fun ${type}List.swap(i: Int, j: Int) { val t = this.get${type}(j); this.set(j, this.get${type}(i)); this.set(i, t) }")
        }
    }
}

val enumSetTask = tasks.register<GenerateSrcTask>("enum-set") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.add("java.util.EnumSet")
    imports.add("java.util.stream.Stream")

    content {
        val generic = "<reified E : Enum<E>>"

        appendLine("inline fun $generic enumSetOf(): EnumSet<E> = EnumSet.noneOf(E::class.java)")

        for (i in 1..PARAM_ENUMERATION_COUNT) {
            appendLine("inline fun $generic enumSetOf(")
            withIndent {
                repeat(i) {
                    appendLine("e$it: E,")
                }
            }
            appendLine("): EnumSet<E> {")
            withIndent {
                appendLine("val set = EnumSet.noneOf(E::class.java)")
                repeat(i) {
                    appendLine("set.add(e$it)")
                }
            }
            appendLine("return set")
            appendLine("}")
            appendLine()
        }

        fun StringAppendable.toEnumSet(iterateOn: String) {
            appendLine("val set = EnumSet.noneOf(E::class.java)")
            appendLine("for (e in $iterateOn) set.add(e)")
            appendLine("return set")
        }

        appendLine("inline fun $generic enumSetOf(vararg elements: E): EnumSet<E> {")
        withIndent {
            toEnumSet("elements")
        }
        appendLine("}")
        appendLine()

        for (it in arrayOf("Array", "Sequence", "Iterable", "Iterator", "Stream")) {
            val generic1 = if (it == "Array") "<out E>" else "<E>"
            appendLine("inline fun $generic $it$generic1.toEnumSet(): EnumSet<E> {")
            withIndent {
                toEnumSet("this")
            }
            appendLine("}")
            appendLine()
        }

        appendLine("inline fun <reified E : Enum<E>> enumSetAllOf(): EnumSet<E> = EnumSet.allOf(E::class.java)")
        appendLine()
        appendLine("inline fun <reified E : Enum<E>> EnumSet<E>.complement(): EnumSet<E> = EnumSet.complementOf(this)")
    }
}

val enumMapTask = tasks.register<GenerateSrcTask>("enum-map") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.add("java.util.EnumMap")

    content {
        val generic = "<reified K : Enum<K>, V>"

        appendLine("inline fun $generic enumMapOf(): EnumMap<K, V> = EnumMap(K::class.java)")
        appendLine()

        appendLine("inline fun $generic enumMapOf(mapping: (K) -> V): EnumMap<K, V> {")
        withIndent {
            appendLine("val map = EnumMap<K, V>(K::class.java)")
            appendLine("for (k in K::class.java.enumConstants) {")
            withIndent {
                appendLine("map.put(k, mapping(k))")
            }
            appendLine("}")
            appendLine("return map")
        }
        appendLine("}")
        appendLine()

        for (i in 1..PARAM_ENUMERATION_COUNT) {
            appendLine("inline fun $generic enumMapOf(")
            withIndent {
                repeat(i) {
                    appendLine("k$it: K, v$it: V,")
                }
            }
            appendLine("): EnumMap<K, V> {")
            withIndent {
                appendLine("val map = EnumMap<K, V>(K::class.java)")
                repeat(i) {
                    appendLine("map.put(k$it, v$it)")
                }
            }
            appendLine("return map")
            appendLine("}")
            appendLine()
        }
    }
}

val forEachIsInstanceTask = tasks.register<GenerateSrcTask>("forEachIsInstance") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.add("java.util.stream.Stream")

    content {
        for (receiver in arrayOf("Array<*>", "Iterable<*>", "Iterator<*>", "Sequence<*>", "Stream<*>")) {
            appendLine("/**")
            appendLine(" * Equivalent to `this.filterIsInstance<R>.forEach { action(it) }`")
            appendLine(" */")
            appendLine("inline fun <reified R> $receiver.forEachIsInstance(action: (R) -> Unit) {")
            appendLine("    for (element in this) {")
            appendLine("        if (element is R) action(element)")
            appendLine("    }")
            appendLine("}")
        }
    }
}

val filterIsInstanceToTask = tasks.register<GenerateSrcTask>("filterIsInstanceTo-enhanced") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.add("java.util.stream.Stream")

    content {
        for (receiver in arrayOf("Array<*>", "Iterable<*>", "Iterator<*>", "Sequence<*>", "Stream<*>")) {
            appendLine("/**")
            appendLine(" * Equivalent to `this.filterIsInstance<R>.filter { predicate(it) }`")
            appendLine(" */")
            appendLine("inline fun <reified R, C : MutableCollection<in R>> $receiver.filterIsInstanceTo(destination: C, predicate: (R) -> Boolean): C {")
            appendLine("    for (element in this) {")
            appendLine("        if (element is R && predicate(element)) destination.add(element)")
            appendLine("    }")
            appendLine("    return destination")
            appendLine("}")
        }
    }
}

val functionInvokeTask = tasks.register<GenerateSrcTask>("function-invoke") {
    group = TASK_GROUP

    packageName.set(PACKAGE)
    imports.addAll(IMPORT_ALL)

    content {
        forEachTypes { from ->
            forEachTypes { to ->
                when {
                    from.isGeneric && to.isGeneric -> appendMultiline("inline operator fun <K, V> ${from.typeName}2${to.typeName}Function<K, V>.invoke(key: K): V = get(key)")
                    from.isGeneric -> appendMultiline("inline operator fun <K> ${from.typeName}2${to.typeName}Function<K>.invoke(key: K): ${to.kotlinType} = get${to.typeName}(key)")
                    to.isGeneric -> appendMultiline("inline operator fun <V> ${from.typeName}2${to.typeName}Function<V>.invoke(key: ${from.kotlinType}): V = get(key)")
                    else -> appendMultiline("inline operator fun ${from.typeName}2${to.typeName}Function.invoke(key: ${from.kotlinType}): ${to.kotlinType} = get(key)")
                }
            }
        }
    }
}

val predicateInvokeTask = tasks.register<GenerateSrcTask>("predicate-invoke") {
    group = TASK_GROUP

    packageName.set(PACKAGE)

    content {
        forEachPrimitiveTypes { from ->
            appendMultiline("inline operator fun ${from.packageName}.${from.typeName}Predicate.invoke(value: ${from.kotlinType}): Boolean = test(value)")
        }
    }
}

val consumerInvokeTask = tasks.register<GenerateSrcTask>("consumer-invoke") {
    group = TASK_GROUP

    packageName.set(PACKAGE)

    content {
        forEachPrimitiveTypes { from ->
            appendMultiline("inline operator fun ${from.packageName}.${from.typeName}Consumer.invoke(value: ${from.kotlinType}) = accept(value)")
        }
    }
}

val unaryOperatorInvokeTask = tasks.register<GenerateSrcTask>("unary-operator-invoke") {
    group = TASK_GROUP
    packageName.set(PACKAGE)

    content {
        forEachPrimitiveTypes { from ->
            appendMultiline("inline operator fun ${from.packageName}.${from.typeName}UnaryOperator.invoke(x: ${from.kotlinType}): ${from.kotlinType} = apply(x)")
        }
    }
}

val binaryOperatorInvokeTask = tasks.register<GenerateSrcTask>("binary-operator-invoke") {
    group = TASK_GROUP
    packageName.set(PACKAGE)

    content {
        forEachPrimitiveTypes { from ->
            appendMultiline("inline operator fun ${from.packageName}.${from.typeName}BinaryOperator.invoke(x: ${from.kotlinType}, y: ${from.kotlinType}): ${from.kotlinType} = apply(x, y)")
        }
    }
}
